plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace "ly.img.cesdk.showcase"
    compileSdk 34

    defaultConfig {
        applicationId "ly.img.cesdk.showcase"
        minSdk 23
        targetSdk 34
        versionCode 1
        versionName "1.17.0"
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86_64", "x86"
        }
        resourceConfigurations += ['en']
    }

    sourceSets {
        main {
            java.srcDirs += [
                "../engine-guides-create-scene-from-image-blob",
                "../engine-guides-create-scene-from-image-url",
                "../engine-guides-create-scene-from-scratch",
                "../engine-guides-create-scene-from-video-url",
                "../engine-guides-load-scene-from-blob",
                "../engine-guides-load-scene-from-remote",
                "../engine-guides-load-scene-from-string",
                "../engine-guides-save-scene-to-archive",
                "../engine-guides-save-scene-to-blob",
                "../engine-guides-save-scene-to-string",
                "../engine-guides-spot-colors",
                "../engine-guides-text-with-emojis",
                "../engine-guides-custom-asset-source",
                "../engine-guides-exporting-blocks",
                "../engine-guides-modifying-scenes",
                "../engine-guides-scopes",
                "../engine-guides-store-metadata",
                "../engine-guides-uri-resolver",
                "../engine-guides-using-effects",
                "../engine-guides-using-fills",
                "../engine-guides-text-properties",
                "../engine-guides-cutouts",
                "../engine-guides-video",
                "../engine-guides-using-camera"
            ]
        }
    }

    signingConfigs {
        release {
            storeFile rootProject.file("android-release.keystore")
            storePassword System.getenv("CESDK_AND_KEYSTORE_PWD")
            keyAlias "cesdk-dev"
            keyPassword System.getenv("CESDK_AND_KEYSTORE_PWD")
        }
    }

    buildTypes {
        release {
            def shrinkAndMinifyEnabled = System.getenv("CESDK_SHRINK_AND_MINIFY_ENABLED") == null
                    || System.getenv("CESDK_SHRINK_AND_MINIFY_ENABLED") == "true"
            signingConfig signingConfigs.release
            minifyEnabled shrinkAndMinifyEnabled
            shrinkResources shrinkAndMinifyEnabled
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField "String", "BUILD_NAME", "\"${System.getenv("CESDK_AND_BUILD_NAME") ?: ""}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.7"
    }
}

dependencies {
    implementation project(":showcase-apparel-ui")
    implementation project(":showcase-postcard-ui")
    implementation findProject(":ubq-android-engine") ?: libs.cesdk.engine
    implementation findProject(":ubq-android-camera") ?: libs.cesdk.engine.camera

    implementation libs.camera.core
    implementation libs.camera.camera2
    implementation libs.camera.view
    implementation libs.camera.lifecycle
    implementation libs.camera.video

    implementation platform(libs.compose.bom)
    implementation libs.compose.material3
    implementation libs.compose.activity
    implementation libs.compose.navigation

    implementation(libs.material) {
        because "MaterialComponents DayNight theme"
    }
}
