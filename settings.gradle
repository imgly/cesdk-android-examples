pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven { url "https://artifactory.img.ly/artifactory/maven" }
    }
}

rootProject.name = "CE.SDK Showcases"
include ':app'
include ':editor-core'
include ':editor-core-ui'
include ':editor-base'
include ':editor-apparel'
include ':editor-postcard'
include ':editor-creative'
include ':editor'

// Internal use only
def useLocalEngine = {
    def propertiesFile = new File(rootDir, "local.properties")
    if (!propertiesFile.exists()) return false
    def properties = new Properties()
    properties.load(propertiesFile.newDataInputStream())
    properties.containsKey("useLocalEngine") && properties.getProperty("useLocalEngine").toBoolean()
}

if (System.getenv("UBQ_USE_LOCAL_ENGINE") == "true" || useLocalEngine()) {
    include ':ubq-android-engine'
    project(':ubq-android-engine').projectDir = new File('../../bindings/android')
    include ':ubq-android-camera'
    project(':ubq-android-camera').projectDir = new File('../cesdk_android_camera')
}
// Internal use only
