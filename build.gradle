import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version libs.versions.gradleAndroid apply false
    id 'com.android.library' version libs.versions.gradleAndroid apply false
    id 'org.jetbrains.kotlin.android' version libs.versions.kotlin apply false
    id 'com.google.gms.google-services' version libs.versions.googleServices apply false
    id 'com.dropbox.dropshots' version libs.versions.dropshots apply false
    id 'com.github.gmazzo.buildconfig' version libs.versions.buildConfig apply false
    id 'com.google.firebase.appdistribution' version libs.versions.appDistributionPlugin apply false
    id 'com.google.firebase.crashlytics' version libs.versions.gradleCrashlytics apply false
}

subprojects {
    // To enable Compose compiler metrics and report (used for debugging Composable parameters stability),
    // set `debugComposeCompilerStability=true` in gradle.properties
    if ("$debugComposeCompilerStability" == "true") {
        tasks.withType(KotlinCompile).configureEach {
            kotlinOptions {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]

                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
