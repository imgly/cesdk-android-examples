apply from: "$projectDir/../release/editor-library.gradle"

def localLicense = {
    def propertiesFile = new File(rootDir, "local.properties")
    if (!propertiesFile.exists()) return null
    def properties = new Properties()
    properties.load(propertiesFile.newDataInputStream())
    properties.getProperty("license")
}

android {
    namespace 'ly.img.editor.core.ui'

    buildTypes.each {
        it.buildConfigField 'String', 'LICENSE', "\"${System.env.CESDK_APP_STORE_LICENSE ?: localLicense() ?: ""}\""
        it.buildConfigField 'String', 'UNSPLASH_HOST', "\"${System.env.CESDK_AND_UNSPLASH_HOST ?: ""}\""
    }
}

dependencies {
    api project(":editor-core")
    api findProject(":ubq-android-engine") ?: libs.cesdk.engine

    implementation platform(libs.compose.bom)
    api libs.compose.material3

    api(libs.compose.activity) {
        because "BackHandler"
    }
    api libs.compose.viewmodel

    api libs.kotlin.coroutines

    implementation libs.coil.compose

    implementation libs.shimmer
}
